buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/repo' }
    }

    dependencies {
        // The Fabric Gradle plugin uses an open ended version to react
        // quickly to Android tooling updates
        classpath 'io.fabric.tools:gradle:1.+'
        //
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

//keystore stuff
//keystore_credentials.properties gitignored for security
def keystoreCredsFile = file('keystore_credentials.properties')
def store_password = "android"
def key_alias = "androiddebugkey"
def key_password = "android"

if (keystoreCredsFile.canRead()) {
    keystoreProps = new Properties()
    keystoreProps.load(new FileInputStream(keystoreCredsFile))

    store_password = keystoreProps['STORE_PASSWORD'].toString()
    key_alias = keystoreProps['KEY_ALIAS'].toString()
    key_password = keystoreProps['KEY_PASSWORD'].toString()
}

def build_date_var = getDate()

//generate version.properties, for keeping track of the build date. For Jenkins S3.
def propFile = new File("app/version.properties");
propFile.createNewFile();
def versionProps = new Properties()
versionProps['BUILD_DATE']=build_date_var
versionProps.store(propFile.newWriter(), null);



android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION
    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode 2
        versionName '1.01'
        testApplicationId 'com.delectable.mobile.tests'
        // API Keys (same for both release and debug)
        buildConfigField 'String', 'KAHUNA_SECRET', '"fffde0bdadbd44c9b3ae78e5ca2d981e"'
        buildConfigField 'String', 'KAHUNA_PUSH_ID', '"1011661250607"'
        buildConfigField 'long', "BUILD_DATE", build_date_var + "L"
    }

    signingConfigs {
        debug {
            storeFile file("../app/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            //release_keystore.jks gitignored for security
            storeFile file("../app/release_keystore.jks")
            storePassword store_password
            keyAlias key_alias
            keyPassword key_password
        }
    }

    buildTypes {
        //constants
        def BOOLEAN = "boolean"
        def TRUE = "true"
        def FALSE = "false"
        def REPORT_CRASHES = "REPORT_CRASHES"

        debug {
            debuggable true
            signingConfig signingConfigs.debug
            buildConfigField BOOLEAN, REPORT_CRASHES, FALSE
        }
        alpha {
            versionNameSuffix "-alpha-" + build_date_var
            debuggable false
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt', '../proguard-com.twitter.sdk.android.txt'
            signingConfig signingConfigs.release
            buildConfigField BOOLEAN, REPORT_CRASHES, TRUE
        }
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt', '../proguard-com.twitter.sdk.android.txt'
            signingConfig signingConfigs.release
            buildConfigField BOOLEAN, REPORT_CRASHES, TRUE
        }
    }
    productFlavors {
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://raw.githubusercontent.com/iainconnor/ObjectCache/master/maven/' }
    maven { url 'https://maven.fabric.io/repo' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile 'junit:junit:4.10@jar'
    compile 'com.android.support:support-v13:19.1'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'org.apache.commons:commons-lang3:3.1@jar'
    compile 'com.squareup.picasso:picasso:2.3.3'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okio:okio:1.0.0'
    compile 'org.ocpsoft.prettytime:prettytime:3.0.2.Final@jar'
    compile project(':facebook')
    compile project(':viewPagerIndicator')
    compile 'com.jakewharton:butterknife:5.1.1'
    //need to keep dagger at 1.1.0 (at least until dagger 2.0 comes out, bc proguard doesn't work with 1.2.2)
    compile 'com.squareup.dagger:dagger:1.1.0'
    compile 'com.squareup.dagger:dagger-compiler:1.1.0'
    compile 'com.jakewharton:disklrucache:2.0.2'
    compile 'com.iainconnor:objectcache:0.0.18-SNAPSHOT@aar'
    compile 'de.greenrobot:eventbus:2.2.1'
    compile 'com.path:android-priority-jobqueue:1.1.2'
    compile 'com.google.android.gms:play-services:5.0.89'
    compile files('libs/kahuna_489.jar')
    compile files('libs/metadata-extractor-2.6.4.jar')
    compile files('libs/xmpcore.jar')

    // Crashlytics Kit
    compile('com.crashlytics.sdk.android:crashlytics:2.0.0@aar') {
        transitive = true
    }

    // Twitter Kit
    compile('com.twitter.sdk.android:twitter:1.0.0@aar') {
        transitive = true
    }

    // MoPub Kit
    compile('com.mopub.sdk.android:mopub:3.1.0@aar') {
        transitive = true
    }
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}